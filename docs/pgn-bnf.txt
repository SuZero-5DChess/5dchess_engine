 <game-tree> ::= <actions> <space-comment>* {'(' <space-comment>* <game-tree>')' <space-comment>*}* [<game-tree> <space-comment>*]

<actions> ::= <turn-serial> <space-comment>* <move> {<space-comment>+ <move>}* <space-comment>*
<turn-serial> ::= <number> ['w' | 'b'] '.' |  '/'
<space-comment> ::= <whitespace> | <comment>
<comment> ::= '{' <comment-text> '}' <whitespace>*
<comment-text> ::= any sequence of words not containing '{' or '}'
<whitespace> ::= {' ' | '\t' | '\n' | '\r'}+

<move> ::= <physical-move> | <superphysical-move>
<physical-move> ::= [<absolute-board>] ([<piece-name>] [<file>] [<rank>] ['x'] <file> <rank> ['=' <promote-to>] | 'O-O' | 'O-O-O')
<superphysical-move> ::= [<absolute-board>] [<piece-name>] [<file>] [<rank>] (<jump-indicator> ['x'] | [<jump-indicator>] ['x'] <to-board>) <file> <rank> ['=' <promote-to>]
<to-board> ::= <absote-board> | <relative-board>
<absolute-board> ::= '(' (['L'] <line> | [['L'] <line>] 'T' <time>) ')'
<relative-board> ::= '$(' (['L'] <difference> | [['L'] <difference>] 'T' <difference>) ')'
<piece-name> ::= 'P' | 'W' | 'K' | 'C' | 'Q' | 'Y' | 'S' | 'N' | 'R' | 'B' | 'U' | 'D'
<promote-to> ::= 'Q' ;change this if promotion to other pieces is allowed
<jump-indicator> ::= '>' | '>>'
<line> ::= ['+' | '-'] <natural-number>
<time> ::= <natural-number>
<difference> ::= '=' | '+' <positive-number> | '-' <positive-number>
<file> ::= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h'
<rank> ::= '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8'
<natural-number> ::= '0' | <positive-number>
<positive-number> ::= '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' | ...